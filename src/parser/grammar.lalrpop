
use crate::parser::tokens::{Token, LexicalError};
use crate::parser::ast;
use std::ops::Not;

grammar<'input>(input: &'input str);

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token<'input> {
    "error" => Token::Error,
    "identifier" => Token::Identifier(<&'input str>),
    "int" => Token::Integer(<i64>),
    "bool" => Token::Boolean(<bool>),
    "if" => Token::If,
    "then" => Token::Then,
    "else" => Token::Else,
    "while" => Token::While(<ast::Position>),
    "do" => Token::Do,
    "skip" => Token::Skip,
    "{" => Token::LCurlyBracket,
    "}" => Token::RCurlyBracket,
    "(" => Token::LParen,
    ")" => Token::RParen,
    ":=" => Token::Assign,
    ";" => Token::Semicolon,
    "+" => Token::OperatorAdd,
    "-" => Token::OperatorSub,
    "*" => Token::OperatorMul,
    "/" => Token::OperatorDiv,
    "=" => Token::Equal,
    "<" => Token::StrictlyLess,
    "&" => Token::And,
    "!" => Token::Not,
    "\n" => Token::Newline
  }
}

pub Statement: Box<ast::Statement<'input>> = {
  #[precedence(level="1")]
  StatementTerm,

  #[precedence(level="2")]
  "if" <guard: BooleanExp> "then" <true_branch: StatementTerm> "else" <false_branch: StatementTerm> => {
    Box::new(ast::Statement::Conditional {
      guard,
      true_branch,
      false_branch
    })
  },
  <pos:"while"> <guard: BooleanExp> "do" <body: StatementTerm> => {
    Box::new(ast::Statement::While{
      pos,
      guard,
      body
    })
  },

  #[precedence(level="3")] #[assoc(side="left")]
  <lhs:Statement> ";" <rhs:Statement> => {
    Box::new(ast::Statement::Composition{ lhs, rhs })
  },
}

pub StatementTerm: Box<ast::Statement<'input>> = {
  <var:"identifier"> ":=" <value:ArithmeticExp> => {
    Box::new(ast::Statement::Assignment(ast::Assignment{var, value}))
  },
  "skip" => Box::new(ast::Statement::Skip),
  "{" <Statement> "}"
}

pub BooleanExp: Box<ast::BooleanExp<'input>> = {
  #[precedence(level="1")]
  BooleanExpTerm,

    "!" <exp: BooleanExp> => {
    Box::new(ast::BooleanExp::not(*exp))
  },

  #[precedence(level="3")] #[assoc(side="left")]
  <lhs: BooleanExp> "&" <rhs: BooleanExp> => {
    Box::new(ast::BooleanExp::And{
      lhs,
      rhs
    })
  },
}


pub BooleanExpTerm: Box<ast::BooleanExp<'input>> = {
  <guard:"bool"> => Box::new(ast::BooleanExp::Boolean(guard)),

  <lhs: ArithmeticExp> "<" <rhs: ArithmeticExp> => {
    Box::new(ast::BooleanExp::ArithmeticCondition(ast::ArithmeticCondition::normal_form(
      lhs,
      ast::ConditionOperator::StrictlyLess,
      rhs
    )))
  },

  <lhs: ArithmeticExp> "=" <rhs: ArithmeticExp> => {
    Box::new(ast::BooleanExp::ArithmeticCondition(ast::ArithmeticCondition::normal_form(
      lhs,
      ast::ConditionOperator::Equal,
      rhs
    )))
  },

  "("<BooleanExp>")",
}

pub ArithmeticExp: Box<ast::ArithmeticExp<'input>> = {
  #[precedence(level="1")]
  Term,

  #[precedence(level="2")] #[assoc(side="left")]
  <lhs:ArithmeticExp> "*" <rhs:ArithmeticExp> => {
    Box::new(ast::ArithmeticExp::BinaryOperation {
      lhs,
      operator: ast::Operator::Mul,
      rhs
    })
  },
  <lhs:ArithmeticExp> "/" <rhs:ArithmeticExp> => {
    Box::new(ast::ArithmeticExp::BinaryOperation {
      lhs,
      operator: ast::Operator::Div,
      rhs
    })
  },

  #[precedence(level="3")] #[assoc(side="left")]
  <lhs:ArithmeticExp> "+" <rhs:ArithmeticExp> => {
    Box::new(ast::ArithmeticExp::BinaryOperation {
      lhs,
      operator: ast::Operator::Add,
      rhs
    })
  },
  <lhs:ArithmeticExp> "-" <rhs:ArithmeticExp> => {
    Box::new(ast::ArithmeticExp::BinaryOperation {
      lhs,
      operator: ast::Operator::Sub,
      rhs
    })
  },
}

pub Term: Box<ast::ArithmeticExp<'input>> = {
  <val:"int"> => {
    Box::new(ast::ArithmeticExp::Integer(val))
  },
  "-" <val:"int"> => {
    Box::new(ast::ArithmeticExp::Integer(-val))
  },
  <name:"identifier"> => {
    Box::new(ast::ArithmeticExp::Variable(name))
  },
  "(" <ArithmeticExp> ")",
}

